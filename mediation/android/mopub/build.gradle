buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
    }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def DEFAULT_COMPILE_SDK_VERSION = 27
def DEFAULT_BUILD_TOOLS_VERSION = "27.0.3"
def DEFAULT_MIN_SDK_VERSION     = 16
def DEFAULT_TARGET_SDK_VERSION  = 27

def DEFAULT_MOPUB_SDK_VERSION = "5.6.0"

android {
    compileSdkVersion safeExtGet('compileSdkVersion', DEFAULT_COMPILE_SDK_VERSION)
    buildToolsVersion safeExtGet('buildToolsVersion', DEFAULT_BUILD_TOOLS_VERSION)
    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', DEFAULT_MIN_SDK_VERSION)
        targetSdkVersion safeExtGet('targetSdkVersion', DEFAULT_TARGET_SDK_VERSION)
        versionCode 1
        versionName "1.0"
    }

    sourceSets {
        main {
            java.srcDirs += ["../shared/src"]
        }
    }

    lintOptions {
        abortOnError false
    }
}

repositories {
    jcenter() // includes the MoPub SDK and AVID library
    maven { url "https://s3.amazonaws.com/moat-sdk-builds" }
}

dependencies {
    def mopubSdkVersion = safeExtGet('mopubVersion', DEFAULT_MOPUB_SDK_VERSION)
    implementation 'com.facebook.react:react-native:+'
    implementation("com.mopub:mopub-sdk-banner:${mopubSdkVersion}@aar") {
        transitive = true
    }
    implementation("com.mopub:mopub-sdk-interstitial:${mopubSdkVersion}@aar") {
        transitive = true
    }
    implementation("com.mopub:mopub-sdk-rewardedvideo:${mopubSdkVersion}@aar") {
        transitive = true
    }
}
  